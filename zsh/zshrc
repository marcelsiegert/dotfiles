zsh_cache_home="${XDG_CACHE_HOME:-${HOME}/.cache}"/zsh && mkdir -p "${zsh_cache_home}"
zsh_config_home="${XDG_CONFIG_HOME:-${HOME}/.config}"/zsh && mkdir -p "${zsh_config_home}"
zsh_data_home="${XDG_DATA_HOME:-${HOME}/.local/share}"/zsh && mkdir -p "${zsh_data_home}"

path=(
  "${HOME}"/.local/bin
  ${path}
)

fpath=(
  "${zsh_config_home}"/functions
  ${fpath}
)

HISTFILE="${zsh_data_home}"/histfile
HISTSIZE=10000
SAVEHIST=10000
setopt HIST_IGNORE_ALL_DUPS
setopt HIST_IGNORE_SPACE
setopt HIST_VERIFY
setopt SHARE_HISTORY

eval $(dircolors)
alias ls='ls --color=auto --hyperlink=auto'

autoload -Uz preferred-util
preferred-util EDITOR nvim vim vi
preferred-util PAGER less more
preferred-util VISUAL nvim vim vi

autoload -Uz zle-line-finish && zle -N zle-line-finish
autoload -Uz zle-line-init && zle -N zle-line-init

bindkey -e
(( ${+terminfo[kich1]} )) && bindkey "${terminfo[kich1]}" overwrite-mode
(( ${+terminfo[kdch1]} )) && bindkey "${terminfo[kdch1]}" delete-char
(( ${+terminfo[khome]} )) && bindkey "${terminfo[khome]}" beginning-of-line
(( ${+terminfo[kend]} )) && bindkey "${terminfo[kend]}" end-of-line
(( ${+terminfo[kpp]} )) && bindkey "${terminfo[kpp]}" up-history
(( ${+terminfo[knp]} )) && bindkey "${terminfo[knp]}" down-history
(( ${+terminfo[kcuu1]} )) && bindkey "${terminfo[kcuu1]}" up-line-or-search
(( ${+terminfo[kcud1]} )) && bindkey "${terminfo[kcud1]}" down-line-or-search
(( ${+terminfo[kLFT5]} )) && bindkey "${terminfo[kLFT5]}" emacs-backward-word
(( ${+terminfo[kRIT5]} )) && bindkey "${terminfo[kRIT5]}" emacs-forward-word

autoload -Uz compinit && compinit -d "${zsh_cache_home}"/compdump

autoload -Uz promptinit && promptinit
prompt marcelsiegert

[[ -f "${HOME}"/.zshrc.local ]] && source "${HOME}"/.zshrc.local || true
