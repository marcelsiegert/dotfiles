zmodload zsh/complist

zsh_cache_home="${XDG_CACHE_HOME:-${HOME}/.cache}"/zsh && mkdir -p "${zsh_cache_home}"
zsh_config_home="${XDG_CONFIG_HOME:-${HOME}/.config}"/zsh && mkdir -p "${zsh_config_home}"
zsh_data_home="${XDG_DATA_HOME:-${HOME}/.local/share}"/zsh && mkdir -p "${zsh_data_home}"

path=(
  "${HOME}"/.local/bin
  ${path}
)

fpath=(
  "${zsh_config_home}"/functions
  ${fpath}
)

HISTFILE="${zsh_data_home}"/histfile
HISTSIZE=10000
SAVEHIST=10000
setopt HIST_IGNORE_ALL_DUPS
setopt HIST_IGNORE_SPACE
setopt HIST_VERIFY
setopt SHARE_HISTORY

DIRSTACKSIZE=10
setopt AUTO_PUSHD
setopt CD_SILENT
setopt PUSHD_IGNORE_DUPS
alias d='dirs -v'
alias -- -='cd +1'
for dirstack_index in {0..$(( DIRSTACKSIZE - 1 ))}; do
  alias "${dirstack_index}"="cd +${dirstack_index}"
done; unset dirstack_index

eval $(dircolors)
alias ls='ls --color=auto --hyperlink=auto'
alias l='ls -ahlN'

export GREP_COLORS='mt=1;44:cx=90'
alias grep='grep --color=auto'

alias diff='diff --color=auto'

export LESS='MR'

autoload -Uz preferred-util
preferred-util EDITOR nvim vim vi
preferred-util PAGER less more
preferred-util VISUAL nvim vim vi

source "${zsh_config_home}"/vendor/zsh-history-substring-search/zsh-history-substring-search.zsh
HISTORY_SUBSTRING_SEARCH_FUZZY='true'
HISTORY_SUBSTRING_SEARCH_GLOBBING_FLAGS='l'
HISTORY_SUBSTRING_SEARCH_HIGHLIGHT_FOUND='standout'
HISTORY_SUBSTRING_SEARCH_HIGHLIGHT_NOT_FOUND='fg=red'

autoload -Uz cd-parent-dir && zle -N cd-parent-dir
autoload -Uz edit-command-line && zle -N edit-command-line
autoload -Uz prepend-sudo && zle -N prepend-sudo
autoload -Uz zle-line-finish && zle -N zle-line-finish
autoload -Uz zle-line-init && zle -N zle-line-init

bindkey -e
bindkey -M emacs '^[m' copy-prev-shell-word
bindkey -M emacs '^X^E' edit-command-line
bindkey -M emacs '\e\e' prepend-sudo
(( ${+terminfo[kich1]} )) && bindkey -M emacs "${terminfo[kich1]}" overwrite-mode
(( ${+terminfo[kdch1]} )) && bindkey -M emacs "${terminfo[kdch1]}" delete-char
(( ${+terminfo[khome]} )) && bindkey -M emacs "${terminfo[khome]}" beginning-of-line
(( ${+terminfo[kend]} )) && bindkey -M emacs "${terminfo[kend]}" end-of-line
(( ${+terminfo[kpp]} )) && bindkey -M emacs "${terminfo[kpp]}" up-history
(( ${+terminfo[knp]} )) && bindkey -M emacs "${terminfo[knp]}" down-history
(( ${+terminfo[kcuu1]} )) && bindkey -M emacs "${terminfo[kcuu1]}" history-substring-search-up
(( ${+terminfo[kcud1]} )) && bindkey -M emacs "${terminfo[kcud1]}" history-substring-search-down
(( ${+terminfo[kLFT5]} )) && bindkey -M emacs "${terminfo[kLFT5]}" emacs-backward-word
(( ${+terminfo[kRIT5]} )) && bindkey -M emacs "${terminfo[kRIT5]}" emacs-forward-word
(( ${+terminfo[kUP3]} )) && bindkey -M emacs "${terminfo[kUP3]}" cd-parent-dir
(( ${+terminfo[kcbt]} )) && bindkey -M menuselect "${terminfo[kcbt]}" reverse-menu-complete

autoload -Uz compinit && compinit -d "${zsh_cache_home}"/compdump
zstyle ':completion:*' cache-path "${zsh_cache_home}"/compcache
zstyle ':completion:*' group-name ''
zstyle ':completion:*' ignore-parents parent pwd
zstyle ':completion:*' list-colors ${(s.:.)LS_COLORS}
zstyle ':completion:*' list-prompt '%SAt %p: Hit TAB for more, or the character to insert%s'
zstyle ':completion:*' matcher-list 'm:{[:lower:]}={[:upper:]}' '+m:{-_}={_-}' '+l:|=* r:|=*'
zstyle ':completion:*' menu select
zstyle ':completion:*' select-prompt '%SScrolling active: current selection at %p%s'
zstyle ':completion:*' use-cache true
zstyle ':completion:*:descriptions' format ' %F{yellow}-- %d --%f'
zstyle ':completion:*:messages' format ' %F{purple}-- %d --%f'
zstyle ':completion:*:warnings' format ' %F{red}-- no matches --%f'
zstyle ':completion:*:manuals.*' insert-sections true
zstyle ':completion:*:manuals' separate-sections true
zstyle ':completion:*:processes' command 'ps -wwu "${USER}" -o pid,args'
zstyle ':completion:*:processes-names' command 'ps -wwu "${USER}" -o comm | uniq'

autoload -Uz promptinit && promptinit
prompt marcelsiegert

[[ -f "${HOME}"/.zshrc.local ]] && source "${HOME}"/.zshrc.local || true
